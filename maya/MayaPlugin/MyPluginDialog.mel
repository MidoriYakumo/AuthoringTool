if( `window -exists myWindow` ) deleteUI myWindow;
window -title "Figure Reshaper" myWindow;
    columnLayout;

        rowLayout -numberOfColumns 2
                  -columnWidth2 340 340
                  -columnAlign2 "left" "right";
            text -label "Step 1: Load Image" -font "boldLabelFont";
            button -label "Browse" -command "myLoadImage()";
        setParent ..;

        rowLayout -numberOfColumns 2
                  -columnWidth2 340 340
                  -columnAlign2 "left" "right";
            text -label "Step 2: Load Template Model" -font "boldLabelFont";
            button -label "Browse" -command "myLoadTemplateModel()";
        setParent ..;

        rowLayout -numberOfColumns 2
                  -columnWidth2 340 340
                  -columnAlign2 "left" "right";
            text -label "Step 3: Automatic Rigging" -font "boldLabelFont";
            button -label "Rig" -command "myAutomaticRigging()";
        setParent ..;

        frameLayout -label "Step 4: Shape Fitting"
                    -collapsable true;
            columnLayout -rowSpacing 5;
                floatSliderGrp -field true -min 30 -max 200 -value 80 -label "Weight" "myWeight";
                floatSliderGrp -field true -min 100 -max 250 -value 190 -label "Height" "myHeight";
                floatSliderGrp -field true -min 0 -max 1 -value 0.5 -label "Girth" "myGirth";
            setParent ..;
        setParent ..;

        rowLayout -numberOfColumns 2
                  -columnWidth2 340 340
                  -columnAlign2 "left" "right";
            button -label "Update" -command "myUpdate()";
            button -label "Cancel" -command "myCancel()";
        setParent ..;

showWindow myWindow;

global proc myLoadImage(){

    string $file = `fileDialog`;

    //---load image---
	imagePlane;
	setAttr imagePlane1.imageName -type "string" $file;
	int $mySize[] = `imagePlane -q -iz imagePlane1`;
	float $myW = $mySize[0] * 1.0 / $mySize[1];
	setAttr "imagePlane1.scaleX" $myW;
	setAttr "imagePlane1.translateZ" -1;
}

global string $gMyBodyModel;

global proc myLoadTemplateModel(){

    global string $gMyBodyModel;

    //---load template model---
	$gMyBodyModel = `fileDialog`; //存model的路径
    file -import
        -type "OBJ"
        -ignoreVersion
        -ra true
        -mergeNamespacesOnClash false
        -namespace "body"
        -options "mo=1"
        -pr $gMyBodyModel;    //file所对应的obj信息存进来
    setAttr "body:Mesh.rotateX" -90;
}

global proc myAutomaticRigging(){

	global string $gMyBodyModel;

    //---automatic rigging---
	FigureReshaper -f $gMyBodyModel; //FigureReshaper -file $gMyBodyModel
}

global proc myUpdate(){

	global string $gMyBodyModel;
    float $w = `floatSliderGrp -query -value "myWeight"`;
    float $h = `floatSliderGrp -query -value "myHeight"`;
    float $g = `floatSliderGrp -query -value "myGirth"`;

    //---update---
	loadPlugin objExport;
	string $filename = pathpart( $gMyBodyModel ) + "/out.obj";
    select -r body:Mesh;
	file -force -es -type "OBJexport" -options "group=0;ptgroups=0;materials=0;smoothing=0;normals=0" -pr $filename;
    MorphShape -f $filename -h $h -w $w;
}

global proc myCancel(){
    deleteUI myWindow;
}

global proc string pathpart( string $path ){

  string $dir = match( "^.*/", $path );
  int $sz = size( $dir );
  
  if ( ( $sz > 1 ) && ( substring( $dir, $sz, $sz ) == "/" ) ) {
    $dir = substring( $dir, 1, ($sz - 1) );
  }
  
  return $dir;
}
